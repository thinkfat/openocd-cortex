# Freescale i.MX6 series single/dual/quad core processor

if { [info exists CHIPNAME] } {
   set  _CHIPNAME $CHIPNAME
} else {
   set  _CHIPNAME imx6
}

# CoreSight Debug Access Port
if { [info exists DAP_TAPID] } {
        set _DAP_TAPID $DAP_TAPID
} else {
        set _DAP_TAPID 0x4ba00477
}

jtag newtap $_CHIPNAME dap -irlen 4 -ircapture 0x01 -irmask 0x0f \
        -expected-id $_DAP_TAPID

# SDMA / no IDCODE
jtag newtap $_CHIPNAME sdma -irlen 4 -ircapture 0x00 -irmask 0x0f

# System JTAG Controller
set _SJC_TAPID_S	0x0191b01d
set _SJC_TAPID_DL	0x0191a01d
set _SJC_TAPID_D	0x0191e01d
set _SJC_TAPID_Q	0x0191c01d

if { [info exists SJC_TAPID ] } {
        set _SJC_TAPID $SJC_TAPID
} else {
	switch $_CHIPNAME {
		imx6s	{ set _SJC_TAPID $_SJC_TAPID_S }
		imx6dl	{ set _SJC_TAPID $_SJC_TAPID_DL }
		imx6d	{ set _SJC_TAPID $_SJC_TAPID_D }
		imx6q	{ set _SJC_TAPID $_SJC_TAPID_Q }
		default	{ set _SJC_TAPID $_SJC_TAPID_Q }
	}
}

jtag newtap $_CHIPNAME sjc -irlen 5 -ircapture 0x01 -irmask 0x1f \
	-expected-id $_SJC_TAPID -ignore-version

# GDB target: Cortex-A9, using DAP, configuring only one core
# Base addresses of core debug interfaces:
# core 0  -  0x02150000
# core 1  -  0x02152000
# core 2  -  0x02154000
# core 3  -  0x02156000
set _TARGETNAME $_CHIPNAME.cpu
target create $_TARGETNAME.0 cortex_a \
	-chain-position $_CHIPNAME.dap \
	-coreid 0 -dbgbase 0x02150000

if { [ info exists SMP ] } {
	switch $SMP {
		2 {
			target create $_TARGETNAME.1 cortex_a \
				-chain-position $_CHIPNAME.dap \
				-coreid 1 -dbgbase 0x02152000
			target smp $_TARGETNAME.1 $_TARGETNAME.0
		}
		4 {
			target create $_TARGETNAME.1 cortex_a \
				-chain-position $_CHIPNAME.dap \
				-coreid 1 -dbgbase 0x02152000
			target create $_TARGETNAME.2 cortex_a \
				-chain-position $_CHIPNAME.dap \
				-coreid 2 -dbgbase 0x02154000
			target create $_TARGETNAME.3 cortex_a \
				-chain-position $_CHIPNAME.dap \
				-coreid 3 -dbgbase 0x02156000
			target smp $_TARGETNAME.3 $_TARGETNAME.2 \
				$_TARGETNAME.1 $_TARGETNAME.0
		}
	}
}

# some TCK cycles are required to activate the DEBUG power domain
jtag configure $_CHIPNAME.sjc -event post-reset "runtest 100"

proc imx6_dbginit {target} {
        # General Cortex A8/A9 debug initialisation
        cortex_a dbginit
}

# Slow speed to be sure it will work
adapter_khz 1000

if { [ info exists SMP ] } { set _cores $SMP } else { set _cores 1 }
for { set _core 0 } { $_core < $_cores } { incr _core 1 } {
	$_TARGETNAME.$_core configure \
		-event reset-start { adapter_khz 1000 }
	$_TARGETNAME.$_core configure \
		-event reset-assert-post "imx6_dbginit $_TARGETNAME.$_core"
	$_TARGETNAME.$_core configure \
		-event gdb-attach { halt }
}
